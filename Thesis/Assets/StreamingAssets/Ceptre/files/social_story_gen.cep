%% prom week-inspired dating sim

character : type.
interest : type.

% world state
present character : pred.
has_interest character interest : bwd.

% social state
affection character character : pred.
attraction character character : pred.
anger character character : pred.
sad character : pred.
bored character : pred.

% actions:
% share interest
% bully
% flirt
% hit on
% confide
% bicker
% insult
% become friends, enemies, dating
% break up, make up, break friendship

resolution character : pred. % used to determine when the story should end.

stage doStuff = {
%% for each of attraction, affection, and anger, there are corresponding
% states that imply a feedback loop (dating, friends, and enemies).
% also there should be at least one action that depends on that state
% and one that nullifies it.

% actions dealing with attraction
do/fallInLove : 
  $present C * attraction C C' -o affection C C'.

do/eroticize : 
  $present C * $affection C C' * affection C C' * affection C C'
  -o attraction C C'.

do/date :
  $present C * $present C' * $affection C C' * $attraction C C'
    * $affection C' C * $attraction C' C
    -o resolution C * resolution C'.

do/flirt :
  $present C * $present C' * $attraction C C' -o attraction C' C.

do/flirt/conflict :
  $present C * $present C' * $present C'' * $attraction C C' * $attraction C'' C
  -o attraction C' C * anger C'' C * anger C'' C' 
    * sad C''.

% actions dealing with friendship

do/shareInterest :
  $present C * $present C' * has_interest C Interest * has_interest C' Interest
  -o affection C C' * affection C' C.

do/comfort :
  $present C * $present C' * sad C * $affection C' C
    -o affection C C' * resolution C.

% introducing some spontaneity
do/like/bored :
  $present C * $present C' * bored C
    -o affection C C'.
do/anger/bored :
  $present C * $present C' * bored C
    -o anger C C'.
do/crush/bored :
  $present C * $present C' * bored C
    -o attraction C C'.

% actions dealing with enmity/anger

do/reject :
  $present C * $present C' * attraction C C' * $anger C' C
    -o sad C.

do/bicker :
  $present C * $present C' * $attraction C C' * $attraction C' C
    -o anger C C' * anger C' C.

do/insult :
  $present C * $present C' * $anger C C'
  -o anger C' C * sad C'.

% termination
do/end : resolution C * present C -o ().
  
}

% story instantiation

chloe : character.
zack : character.
melissa : character.
zoe : character.
lil : character.
charles : character.

% interests
math : interest.
music : interest.
fashion : interest.
books : interest.

has_interest zack math.
has_interest zack fashion.
has_interest melissa fashion.
has_interest melissa books.
has_interest zoe math.
has_interest zoe music.
has_interest lil music.
has_interest lil fashion.
has_interest charles books.
has_interest charles math.

context init =
{ present chloe,
  present zack,
  present melissa,
  present zoe,
  present lil,
  present charles,
  bored chloe,
  bored zack,
  bored melissa,
  bored zoe,
  bored lil,
  bored charles
}.

#trace _ doStuff init.
