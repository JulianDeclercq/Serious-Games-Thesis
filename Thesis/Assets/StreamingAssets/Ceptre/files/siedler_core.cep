

brick : pred.
lumber : pred.
road : pred.
wool : pred.
grain : pred.
settlement : pred.
ore : pred.
city : pred.
development_card : pred.
knight : pred.
progress : pred.
victory_point : pred.

turn : pred.
success : pred.

stage building = {

build_road : turn * brick * lumber -o road * success.
build_settlement : turn * brick * lumber * wool * grain 
  -o settlement * success.
build_city : turn * ore * ore * ore * grain * grain 
  -o city * success.
build_development : turn * ore * wool * grain 
  -o development_card * success.

}
% #interactive building.

qui * stage building * success -o stage draw_dev_card.
qui * stage building * turn -o ().

stage draw_dev_card = {

dev_knight : development_card -o knight.
dev_prog : development_card -o progress.
dev_victory_point : development_card -o victory_point.

}

qui * stage draw_dev_card -o stage building * turn.

context init =
{turn,
  brick, brick, brick, lumber, lumber, lumber, wool, wool, wool, 
  grain, grain, grain, ore, ore, ore}.

#trace _ building init.
