% tiny garden sim
% this version is fairly easy and repetitive.
% nothing really pushes back against your efforts to maintain the
% garden except that things die if you don't water them.
% also there are no real interesting decisions - all the plants are the
% same.

plant_type : type.
flower : plant_type.
herb : plant_type.
vine : plant_type.

plant_phase : type.
seed : plant_phase.
seedling : plant_phase.
mature : plant_phase.

next plant_phase plant_phase : bwd.
next seed seedling.
next seedling mature.

health : type.
healthy : health.
overgrowing : health.
wilting : health.
dead : health.

harvested plant_type : pred.
have_seed plant_type : pred.
plant plant_type plant_phase health : pred.

step : pred.

soil_space : pred.

% actions: water, plant, harvest
stage play {
  get_seeds : $step * harvested Type -o have_seed Type * have_seed Type.
  plant : $step * have_seed Type * soil_space -o plant Type seed healthy.
  harvest : $step * plant Type mature healthy -o harvested Type * soil_space.
  clear : $step * plant Type _ dead -o soil_space.
  water : $step * plant Type Phase wilting -o plant Type Phase healthy.
  done : step -o ().
}
#interactive play.

qui * stage play -o stage grow * step.

stage grow = {
  grow : $step * plant Type Phase healthy * next Phase Phase'
            -o plant Type Phase' healthy.
  wilt : $step * plant Type Phase healthy
            -o plant Type Phase wilting.
  die : $step * plant Type Phase wilting
            -o plant Type Phase dead.
  done : step -o ().
}
qui * stage grow -o stage play * step.

context init = {have_seed flower, have_seed herb, have_seed vine, step,
                  soil_space, soil_space, soil_space, soil_space}.
#trace _ play init.

